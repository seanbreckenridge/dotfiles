#!/bin/zsh
#compdef plainplay

# autocompletion for https://github.com/purarue/plaintext-playlist

# to enable this, put the following in your zsh config:
#
# # just some aliases I like
# alias play='plainplay'
# alias pplay='plainplay play'
# alias splay='plainplay shuffle'
#
# autoload -Uz compinit && compinit  # if not already done somewhere else
# source ~/.config/zsh/completion/_plainplay # (this file, put it on your system somewhere)
#
# compdef _plainplay plainplay play
# compdef _playlist_files splay pplay

function _playlist_commands() {
	plainplay help | awk -n '/^add.*$/,0 { printf $1 " " }'
}

function _playlist_names() {
	local PLAYLIST_DIR="${PLAINTEXT_PLAYLIST_PLAYLISTS:-${XDG_DATA_HOME:-$HOME/.local/share}/plaintext_playlist}"
	find "$PLAYLIST_DIR" -iname "*.txt" -printf '%f\n' | cut -d'.' -f1 | xargs echo -n
}

# for plainplay/play
function _plainplay() {
	local plist_names plist_cmds
	plist_cmds="$(_playlist_commands)"
	plist_names="$(_playlist_names)"
	_arguments \
		"1:command:(${plist_cmds})" \
		"*:playlist names:(${plist_names})"
}

# for splay/pplay aliases
function _playlist_files() {
	local plist_names
	plist_names="$(_playlist_names)"
	_alternative \
		"playlists:playlist names:(${plist_names})"
}
